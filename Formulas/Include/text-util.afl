/************************************************
* Utility functions that work on text data.
*
* Author: Pritesh Mhatre
************************************************/

/**
* AmiBroker substring function. Returns a string that is a substring of the string. 
* Implementation of Java substring function in AmiBroker.
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strSubstring
*/
function strSubstring(text, beginIndex, endIndex) {

	result = "";
	length = strlen(text);

	// Perform validation
	if(beginIndex < 0 OR endIndex > length OR beginIndex > endIndex) {
		_TRACE("Function: strSubstring, Error: Validation failure for string: " + text);
		result = "";
	} else {
		// Calculate the result
		count = endIndex - beginIndex;	
		result = StrMid(text, beginIndex, count);
	}
	
	return result;
}

/**
* A function that returns first N characters of a string.
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strFirstN
*/
function strFirstN(text, n) {

	result = "";
		
	// Perform validation
	if(n < 0) {
		_TRACE("Function: strFirstN, Error: Validation failure for string: " + text);
		result = "";
	} else {
		// Calculate the result
		result = StrMid(text, 0, n);
	}
	
	return result;
}


/**
* A function that returns last N characters of a string.
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strLastN
*/
function strLastN(text, n) {

	result = "";
	length = strlen(text);
	
	// Perform validation
	if(n < 0 OR n > length) {
		_TRACE("Function: strLastN, Error: Validation failure for string: " + text);
		result = "";
	} else {
		// Calculate the result
		start = length - n;
		result = StrMid(text, start, n);
	}
	
	return result;
}

/**
* A function to find whether two strings are equal (case-sensitive).
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strEquals
*/
function strEquals(a, b) {
	return (a == b);
}

/**
* A function to find whether two strings are equal (case-insensitive).
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strEqualsIgnoreCase
*/
function strEqualsIgnoreCase(a, b) {
	aLower = StrToLower(a);
	bLower = StrToLower(b);
	
	return (aLower == bLower);
}

/**
* A function to find whether a string starts with specified prefix.
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strStartsWith
*/
function strStartsWith(text, prefix) {
	beginning = strFirstN(text, strlen(prefix));
	
	return strEquals(beginning, prefix);
}

/**
* A function to find whether a string ends with specified suffix.
*
* Documentation: https://github.com/Pritesh-Mhatre/amibroker-library/wiki/Text-Utility-Functions#strEndsWith
*/
function strEndsWith(text, suffix) {
	ending = strLastN(text, strlen(suffix));
	
	return strEquals(ending, suffix);
}
